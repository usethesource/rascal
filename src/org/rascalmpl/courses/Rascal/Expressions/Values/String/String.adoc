[[Values-String]]
# String
:concept: Expressions/Values/String

.Synopsis
String values.

.Syntax
`"_StringChar_~1~_StringChar_~2~..."`
where `_StringChar~i~_ may be one of the following:

*  __Ordinary character__: Any character except `<`, `>`, `"`, `'` or `\`.
*  __Escaped character__: Backslash `\` followed by any of  `<`, `>`, `"`, `'` or `\` represents the escaped character itself.
  Other escape sequences that are supported are:
**  `\n`: newline
**  `\t`: tab
**  `\r`: carriage return
**  `\b`: backspace
**  `\f`: vertical feed
**  `\u _hexDigit~1~_ _hexDigit~2~_ _hexDigit~3~_ _hexDigit~4~_` : hexadecimal escapes with four digit indexes into UNICODE.
**  `\U _hexDigit~1~_ _hexDigit~2~_ _hexDigit~3~_ _hexDigit~4~_ _hexDigit~5~_ _hexDigit~6~_` : hexadecimal escapes with six digit indexes into UNICODE.
**  `\ a_hexDigit~1~_ _hexDigit~2~_`:  hexadecimal escapes with 2 digit indexes into ASCII (0x0 ... 0x7F).


*  __String Interpolation__: 

|====
| Form      | Description

| `<_Exp_>` | Interpolate the value of the expression as a string
| `<if(_Exp_){> ... _StringChars_ ... <}>` | Conditional inclusion of _Text_, where _StringChars_ may use variables introduced in _Exp_
| `<if(_Exp_){> ... _StringChars~1~_ ... <} else {>  ... _StringChars~2~_ ... <}>` | Conditional inclusion of either _StringChars~1~_ or _StringChars~2~_
| `<for(_Exp_){>... _StringChars_ ... <}>` | Iterative splicing of _StringChars_ into the result, where _StringChars_ may use variables introduced in _Exp_.
| `<while(_Exp_){> ... _StringChars_ ... <}>` | Iterative splicing of _StringChars_ into the result, where _StringChars_ may use variables introduced in _Exp_.
| `<do {>... _StringChars_ ... <} while (_Exp_)>` | Iterative splicing of _StringChars_ into the result, where _StringChars_ may use variables introduced in _Exp_.
|====

*  __Multiline__:

|====
| Form | Description 

| `_StringChars~1~_\n _StringChars~2~_ `   | Strings can be multi-line without an escape or continuation marker 
| `_StringChars~2~_\n '  _StringChars~2~_` | A margin character `'` indicates where the next line starts 
|====


.Usage

.Types
`str`

.Function

.Description

The string values are represented by the type `str` and consist of character 
sequences surrounded by double quotes, e.g., `"a"` or `"a\nlong\nstring"`.

_Multiline_: Strings may span more than one line. The margin character `'` indicates which part of a line will be ignored. This is useful for indenting a multi-line string with the source code that generates it.

_Interpolation_:  String literals support so-called _string interpolation_: 
inside string constants text between angle brackets (`<` and `>`) is first executed and then replaced by
 its string value.
Various statements (if, for, while, do) also return a value and can be used in this way.
In the interpolation variant of these statements the block or blocks that are part of the statement become arbitrary text 
(that may itself contain interpolations). 

_Auto-indent_: Expressions that get interpolated in a string will be auto-indented. This means that each line that results from the evaluation of the expression is prefixed with the indentation level of the position of the expression in the current string.

The following operators are defined for Strings:

**** <<String-Concatenation,String Concatenation>>: Concatenate two strings.
**** <<String-Equal,String Equal>>: Equality operator on string values.
**** <<String-GreaterThan,String GreaterThan>>: Greater than operator on string values.
**** <<String-GreaterThanOrEqual,String GreaterThanOrEqual>>: Greater than or equal operator on string values.
**** <<String-LessThan,String LessThan>>: Less than operator on string values.
**** <<String-LessThanOrEqual,String LessThanOrEqual>>: Less than or equal operator on string values.
**** <<String-NotEqual,String NotEqual>>: Not equal operator on string values.
**** <<String-Slice,String Slice>>: Retrieve a slice of a string.
**** <<String-Subscription,String Subscription>>: Retrieve a substring via its index.

The following functions are provided for strings:
subtoc[Rascal/Expressions/Values/String,1]

.Examples
[source,rascal-shell]
----
rascal>N = 13;
int: 13
rascal>"The value of N is <N>";
str: "The value of N is 13"
rascal>"The value of N*N is <N*N>";
str: "The value of N*N is 169"
rascal>"The value is <(N < 10) ? 10 : N*N>";
str: "The value is 169"
----
As you can see the string value of variables and expressions is interpolated in the result as expected. 
<br>
Some examples of more advances string interpolation 
[source,rascal-shell]
----
rascal>"N is <if(N < 10){> small <} else {> large <}>";
str: "N is  large "
rascal>"N is <if(N < 10){> small <} else {> large (<N>)<}>";
str: "N is  large (13)"
rascal>"before <for(x<-[1..5]){>a <x> b <}>after";
str: "before a 1 b a 2 b a 3 b a 4 b after"
----
multi-line string
[source,rascal-shell]
----
rascal>import IO;
ok
rascal>println("hello
>>>>>>>this
>>>>>>>  is
>>>>>>>    new")
hello
this
  is
    new
ok
----
multi-line string with margin:
[source,rascal-shell]
----
rascal>if (true)
>>>>>>>  println("this is
>>>>>>>          'what
>>>>>>>          '  margins
>>>>>>>          'are good for
>>>>>>>          ");
this is
what
  margins
are good for
          
ok
----
auto indent:
[source,rascal-shell]
----
rascal>str genMethod(str n) = "int <n>() {
>>>>>>>                       '  return 0;
>>>>>>>                       '}";
ok
rascal>str genClass() = "class myClass {
>>>>>>>                 '  <genMethod("myMethod")>
>>>>>>>                 '}";
ok
rascal>println(genClass());
class myClass {
  int myMethod() {
    return 0;
  }
}
ok
----


.Benefits
String interpolation enables very flexible template-based text generation as used in generators for
source code, markup and the like.

.Pitfalls


:leveloffset: +1
include::Concatenation/Concatenation.adoc[Concatenation]

include::Equal/Equal.adoc[Equal]

include::GreaterThan/GreaterThan.adoc[GreaterThan]

include::GreaterThanOrEqual/GreaterThanOrEqual.adoc[GreaterThanOrEqual]

include::LessThan/LessThan.adoc[LessThan]

include::LessThanOrEqual/LessThanOrEqual.adoc[LessThanOrEqual]

include::NotEqual/NotEqual.adoc[NotEqual]

include::Slice/Slice.adoc[Slice]

include::Subscription/Subscription.adoc[Subscription]


:leveloffset: -1